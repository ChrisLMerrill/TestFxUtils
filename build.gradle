plugins {
    id "java"
    id "maven-publish"
    id "com.jfrog.bintray" version "1.8.4"
    id 'org.openjfx.javafxplugin' version '0.0.8'
}

version 2.0
group = 'net.christophermerrill'

repositories
    {
    jcenter()
    }

javafx {
    version = "14.0.1"
    modules = [ 'javafx.controls' ]
}

dependencies
    {
    compile 'org.junit.jupiter:junit-jupiter-api:5.5.1'
    compile 'org.testfx:testfx-core:4.0.16-alpha'
    compile 'org.testfx:testfx-junit5:4.0.16-alpha'
    compile 'org.openjfx:javafx-controls:14.0.1'
    }

javadoc {
    include 'net/christophermerrill/testfx/*'
    options.addBooleanOption('html5', true)
}

task sourcesJar(type: Jar) {
    archive.setClassifier('sources')
    from sourceSets.main.allSource
    }

task javadocJar(type: Jar) {
    archive.setClassifier('javadoc')
    from javadoc
    }

tasks.withType(JavaCompile) {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

artifacts {
    archives sourcesJar
    archives javadocJar
    }

if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
      tasks.withType(Javadoc) {
        options.addStringOption('Xdoclint:none', '-quiet')
      }
    }
  }

wrapper {
    gradleVersion = '6.3'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = 'TestFxUtils' // pom.project.name must be same as bintray.pkg.name
                description = 'Tools for using TestFx to test JavaFX applications'
                url = 'https://github.com/ChrisLMerrill/TestFxUtils'
                packaging = 'jar'
                groupId = project.group
                artifactId = 'TestFxUtils'
                version = project.version
                inceptionYear = '2017' // HARDCODED
                licenses {
                    license { // HARDCODED
                        name = 'Apache-2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0'
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        id = "ChrisLMerrill"
                        name = "Chris Merrill"
                        email = "osdev@christophermerrill.net"
                    }
                }
                scm {
                        connection = 'https://github.com/ChrisLMerrill/TestFxUtils.git'
                        developerConnection = 'https://github.com/ChrisLMerrill'
                        url = 'https://github.com/ChrisLMerrill/TestFxUtils'
                }
            }
        }
    }
}

bintray {
    user = BINTRAY_UPLOAD_USERNAME
    key = BINTRAY_UPLOAD_APIKEY
    publications = ['mavenJava']
    pkg {
        repo = 'maven'
        name = 'TestFxUtils'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/ChrisLMerrill/TestFxUtils.git'
        version {
            name = project.version
            released  = new Date()
            gpg {
                sign = true
                passphrase = GPG_JARSIGN_PASSPHRASE
            }
            mavenCentralSync {
                sync = true //[Default: true] Determines whether to sync the version to Maven Central.
                user = MAVENCENTRAL_USERNAME
                password = MAVENCENTRAL_PASSWORD
                close = '1' //Optional property. By default the staging repository is closed and artifacts are released to Maven Central. You can optionally turn this behaviour off (by puting 0 as value) and release the version manually.
            }
        }
    }

}
